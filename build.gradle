buildscript {
    ext{
        odcPluginVersion = project.hasProperty('odcGradlePluginVersion') ? project.getProperty('odcGradlePluginVersion') : '10.0.3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.owasp:dependency-check-gradle:$odcPluginVersion"
    }
}

apply plugin: 'java'
apply plugin: 'org.owasp.dependencycheck'

group = 'eu.righettod'
version = '2.0.1'
def burpExtensionHomepage = 'https://github.com/righettod/log-requests-to-sqlite'
def burpExtensionJarName = 'LogRequestsToSQLite.jar'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.49.1.0'
    // implementation 'net.portswigger.burp.extender:burp-extender-api:2.3'
    implementation 'net.portswigger.burp.extensions:montoya-api:2025.4'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}

compileJava {
    targetCompatibility  = '21'
    sourceCompatibility = '21'
}

dependencyCheck {
    autoUpdate = true
    failOnError = true
    failBuildOnCVSS = 1
    nvd.validForHours = 12
    suppressionFile = "odc-suppress.xml"
    format = 'JSON'
}

task fatJar(type: Jar) {
    manifest {
        attributes("Implementation-Version": project.version, "Implementation-Title": project.name, "Implementation-URL": burpExtensionHomepage)
    }
    archiveBaseName = burpExtensionJarName
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
